buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath group: "org.eclipse.jgit", name: "org.eclipse.jgit", version: "5.11.1.202105131744-r"
	}
}

plugins {
	id "java-library"
	id "eclipse"
	id "maven-publish"
}

repositories {
	mavenCentral()
}

dependencies {
	implementation "org.apache.logging.log4j:log4j-core:2.17.1"
	annotationProcessor "org.apache.logging.log4j:log4j-core:2.17.1"
}

group = "net.hycrafthd"
archivesBaseName = "logging_util"
version = "1.0.1"

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(8)
	}
	withSourcesJar()
	withJavadocJar()
}

tasks.named("jar") {
	archiveClassifier = ""
	
	from "LICENSE"
	
	manifest {
		attributes(
				"Implementation-Version": project.version,
				"Implementation-Vendor": "HyCraftHD",
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
				)
	}
}

tasks.withType(PublishToMavenRepository) {
	onlyIf {
		getValue("maven.password") != null
	}
}

publishing {
	publish.dependsOn build
	
	repositories {
		maven {
			url "https://repo.u-team.info"
			credentials {
				username = "maven"
				password = getValue("maven.password")
			}
		}
	}
	publications {
		mavenJava(MavenPublication) {
			from components.java
			
			groupId = project.group
			artifactId = project.archivesBaseName
			version = project.version
			
			pom {
				name = project.archivesBaseName
				description = "Minecraft-Downloader"
				url = "https://github.com/HyCraftHD/Logging-Util"
				scm {
					url = "https://github.com/HyCraftHD/Logging-Util"
					connection = "scm:git:git://github.com/HyCraftHD/Logging-Util.git"
					developerConnection = "scm:git:git@github.com:HyCraftHD/Logging-Util.git"
				}
				issueManagement {
					system = "github"
					url = "https://github.com/HyCraftHD/Logging-Util/issues"
				}
				licenses {
					license {
						name = "Apache License 2.0"
						url = "https://github.com/HyCraftHD/Logging-Util/blob/main/LICENSE"
						distribution = "repo"
					}
				}
				developers {
					developer {
						name = "HyCraftHD"
					}
				}
			}
		}
	}
}

def getValue(name) {
	def envVariable = System.getenv(name)
	if(envVariable != null) {
		return envVariable
	} else {
		if (project.hasProperty(name)) {
			return project.getProperty(name)
		}
	}
	return null;
}

task release {
	tasks.release.dependsOn build
	tasks.release.dependsOn publish
	doLast {
		def git = org.eclipse.jgit.api.Git.open(file("${rootDir}"))
		if(git != null) {
			git.tag().setName(project.version).setForceUpdate(true).call()
		}
		println "Released, Need to push tags"
	}
}
